plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0'
    id "org.asciidoctor.jvm.convert" version "3.3.2"
    id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

jacoco {
    toolVersion = "0.8.8"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // MySQL
    implementation 'mysql:mysql-connector-java'
    implementation 'org.apache.tomcat:tomcat-jdbc'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // MyBatis
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter'
    implementation 'org.mybatis:mybatis'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'io.lettuce:lettuce-core'
    implementation 'org.apache.commons:commons-pool2'

    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui'

    // AWS S3
    implementation 'org.springframework.cloud:spring-cloud-starter-aws'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    // JWT
    runtimeOnly 'io.jsonwebtoken:jjwt-impl'
    implementation 'io.jsonwebtoken:jjwt-api'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson'

    // WebSocket
    implementation 'org.springframework.boot:spring-boot-starter-websocket'

    // Selenium
    implementation 'org.seleniumhq.selenium:selenium-java'

    // H2 Database (Runtime Only)
    runtimeOnly 'com.h2database:h2'
}

tasks.named('jar') {
    enabled = false
}

tasks.named('test') {
    useJUnitPlatform()
    jvmArgs '-Xshare:off' // JVM 아규먼트 설정
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
        xml.required = false
        csv.required = false
    }

    // 결과 리포트에서 제외할 클래스들
    def Qdomains = []
    for (qPattern in '**/QA'..'**/QZ') {
        Qdomains.add(qPattern + '*')
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, excludes: [
                            "**/*Application*",
                            "**/*Mapper*",
                            "**/*Provider*",
                            "**/*Config*",
                            "**/*Util*",
                            "**/*Request*",
                            "**/*Response*",
                            "**/*Entity*",
                            "**/common"
                    ] + Qdomains)
                })
        )
    }

    finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
    def Qdomains = []
    for (qPattern in '*.QA'..'*.QZ') {
        Qdomains.add(qPattern + '*')
    }

    violationRules {
        rule {
            enabled = true
            element = 'CLASS'
            includes = []

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
            excludes = [
                    "**.*Application*",
                    "**.*Mapper*",
                    "**.*Provider*",
                    "**.*Config*",
                    "**.*Request*",
                    "**.*Response*",
                    "**.*Entity*"
            ] + Qdomains
        }
    }
}
